#===============================================================================
#
# Target-Level Build Script for Modem Processor
#
# GENERAL DESCRIPTION
#    build script
#
# Copyright (c) 2009-2009 by Qualcomm Technologies, Incorporated.
# All Rights Reserved.
# QUALCOMM Proprietary/GTDR
#
#-------------------------------------------------------------------------------
#
#  $Header: //components/rel/dspbuild.adsp/2.2/bsp/devcfg_img/build/devcfg_img.scons#4 $
#  $DateTime: 2013/07/26 15:03:10 $
#  $Change: 4171534 $
#                      EDIT HISTORY FOR FILE
#
#  This section contains comments describing changes made to the module.
#  Notice that changes are listed in reverse chronological order.
#
# when       who     what, where, why
# --------   ---     ---------------------------------------------------------
#
#===============================================================================
import sys
import os
Import('env')

env = env.Clone()

#------------------------------------------------------------------------------
# Check if we need to load this script or just bail-out
#------------------------------------------------------------------------------
# alias first alias is always the target then the other possibles
aliases = [
   'devcfg_img', 'adsp_images','adsp_mpd_images', 'adsp_mpd_core_images', 'adsp_core_images',
   'msm8974_SPD', 'mdm9x25_SPD', 'msm8x26_SPD', 'msm8x10_SPD',
   'msm8974_MPD', 'mdm9x25_MPD', 'msm8x26_MPD', 'msm8x10_MPD', 'apq8084_MPD', 'msm8962_MPD'
]

# init environment variables
output_elf_name = 'M${BUILD_ASIC}${BUILD_ID}${BUILD_VER}'
env.InitImageVars(
   alias_list=aliases,        # aliases
   proc='qdsp6',              # proc
   config='adsp',         # config type, proc_name
   plat='qurt',
   target= output_elf_name,   # target (elf, image file name)
   build_tags = ['DAL_DEVCFG_IMG'],
   tools = ['buildspec_builder.py',
            '${BUILD_ROOT}/core/bsp/build/scripts/mbn_builder.py',
            '${BUILD_ROOT}/core/bsp/build/scripts/doc_builder.py',
            '${BUILD_ROOT}/core/bsp/build/scripts/dnt_builder.py',
            '${BUILD_ROOT}/core/bsp/build/scripts/cmm_builder.py',
            '${BUILD_ROOT}/core/bsp/build/scripts/devcfg_builder.py',
            '${BUILD_ROOT}/core/bsp/build/scripts/gendevcfghdr.py',
            '${BUILD_ROOT}/core/bsp/build/scripts/elfmergerutil.py',                       

            '${BUILD_ROOT}/core/bsp/build/scripts/devcfg_lcs_builder.py',
            ]
   )

if not env.CheckAlias(alias_list=aliases):
   Return()

#------------------------------------------------------------------------------
# Setting up export paths for access to elfparserutil.py
#------------------------------------------------------------------------------
corebsp_scripts_path = env.RealPath('${BUILD_ROOT}/core/bsp/build/scripts/')
sys.path.append(corebsp_scripts_path)

#------------------------------------------------------------------------------
# Print banner
#------------------------------------------------------------------------------
env.PrintImageBanner()

#---------------------------------------------------------------------------
# Now that we know we need to build something, the first thing we need
# to do is add our image to BuildProducts.txt, so that tools can verify
# when our build is successful.  Make sure we append, so that we don't
# overwrite other images that have written to this file.
#---------------------------------------------------------------------------
#build_products_fn = env['TARGET_ROOT'] + "/BuildProducts.txt"
#if not env.GetOption('clean'):
#   fh = open(build_products_fn, "a")
#   fh.write ("./modem_proc/build/ms/bin/" + env['QC_SHORT_BUILDPATH'] + "/dsp2.mbn\n")
#   fh.close()
#else:
#   if os.path.exists(build_products_fn):
#      os.remove(build_products_fn)

#---------------------------------------------------------------------------
# Load in CBSP uses and path variables
#---------------------------------------------------------------------------
env.InitBuildConfig()
env.Replace(USES_DEVCFG = 'yes') 
env.Replace(DEVCONFIG_ASSOC_FLAG = 'DAL_DEVCFG_IMG')

#---------------------------------------------------------------------------
# Load in the tools scripts
#---------------------------------------------------------------------------
env.Replace(QDSP6_LIBDIR = "${COREBSP_ROOT}/kernel/qurt/install_again/ADSPv5MP/lib")
  
env.LoadToolScript('hexagon_adsp', toolpath = ['${BUILD_ROOT}/qdsp6/tools'])
env.LoadToolScript('qdsp6_defs_adsp', toolpath = ['${BUILD_ROOT}/build/ms'])

if env.get('CHIPSET') == "mdm9x25":
  env.AddUsesFlags('USES_PLATFORM_MINIMAL')
#---------------------------------------------------------------------------
# Load in the compiler options for the devcfg image
#---------------------------------------------------------------------------
# This ensures that there is no .sdata section created in the devcfg image
env.Append(CFLAGS = "-G0 -fno-zero-initialized-in-bss ")

#---------------------------------------------------------------------------
# Libraries/Objects Section
#---------------------------------------------------------------------------
devcfgimg_libs = []
devcfgimg_objs = []
devcfg_units = []

primary_elf = env.RealPath('${BUILD_MS_ROOT}/M${BUILD_ASIC}${BUILD_ID}${BUILD_VER}_NODEVCFG.elf')
devcfg_build_dest_name = '${SHORT_BUILDPATH}/DEVCFG_${BUILD_ASIC}${BUILD_ID}${BUILD_VER}'
devcfg_elf_name = env.RealPath('${BUILD_MS_ROOT}/M${BUILD_ASIC}${BUILD_ID}${BUILD_VER}_DEVCFG.elf')
devcfg_lcs_template_file = env.RealPath('${BUILD_MS_ROOT}/DEVCFG_IMG.lcs.template')
devcfg_lcs_name = 'DEVCFG_IMG.lcs'
devcfg_lcs_file_path_name = env.RealPath('${BUILD_MS_ROOT}/DEVCFG_IMG.lcs')
merged_elf_name_pre_reloc_changes = env.RealPath('${SHORT_BUILDPATH}/M${BUILD_ASIC}${BUILD_ID}${BUILD_VER}_pre_reloc_changes_v.elf')
merged_elf_name = env.RealPath('${SHORT_BUILDPATH}/M${BUILD_ASIC}${BUILD_ID}${BUILD_VER}.elf')
merged_elf_name_output = env.RealPath('${BUILD_MS_ROOT}/' + output_elf_name + '_pre_reloc.elf')
devcfg_hdr_file_name = 'devcfg_def.h'
devcfg_hdr_file = env.RealPath('${BUILD_MS_ROOT}/' + devcfg_hdr_file_name)

# Template to load au_name libs/objs build rules.
#au_name_path = env.RealPath('${BUILD_ROOT}/au_name')
#if env.PathExists(au_name_path):
   #au_name_items = env.LoadAreaSoftwareUnits('au_name')
   #ourimg_libs.extend(au_name_items['LIBS'])
   #ourimg_objs.extend(au_name_items['OBJS'])

# load Core BSP libs/objs build rules.
core_path = env.RealPath('${BUILD_ROOT}/core')
if env.PathExists(core_path):
   au_items = env.LoadAreaSoftwareUnits('core')
   devcfgimg_libs.extend(au_items['LIBS'])
   devcfgimg_objs.extend(au_items['OBJS'])

# load AVS libs/objs build rules.
avs_path = env.RealPath('${BUILD_ROOT}/avs')
if env.PathExists(avs_path):
   au_items = env.LoadAreaSoftwareUnits('avs')
   devcfgimg_libs.extend(au_items['LIBS'])
   devcfgimg_objs.extend(au_items['OBJS'])

# load qdsp6 libs/objs build rules.
qdsp6_path = env.RealPath('${BUILD_ROOT}/qdsp6')
if env.PathExists(qdsp6_path):
   au_items = env.LoadAreaSoftwareUnits('qdsp6')
   devcfgimg_libs.extend(au_items['LIBS'])
   devcfgimg_objs.extend(au_items['OBJS'])

platform_path = env.RealPath('${BUILD_ROOT}/platform')
if env.PathExists(platform_path):
   au_items = env.LoadAreaSoftwareUnits('platform')
   devcfgimg_libs.extend(au_items['LIBS'])
   devcfgimg_objs.extend(au_items['OBJS'])

sensor_path = env.RealPath('${BUILD_ROOT}/Sensors')
if env.PathExists(sensor_path):
   au_items = env.LoadAreaSoftwareUnits('Sensors')
   devcfgimg_libs.extend(au_items['LIBS'])
   devcfgimg_objs.extend(au_items['OBJS'])

devcfg_units = [devcfgimg_libs, devcfgimg_objs]

#------------------------------------------------------------------------------
# Rule for compiling devcfg_main.c 
#------------------------------------------------------------------------------
# filter is not define or filter is current target files only, ok to build misc files
filter_opt = env.get('FILTER_OPT')
if (filter_opt is None) or (env.FilterMatch(os.getcwd())):
   env.VariantDir("${BUILDPATH}", "${BUILD_ROOT}/build/bsp/devcfg_img/src", duplicate=0)
   env.RequirePublicApi(['DAL'])
   env.RequireRestrictedApi(['DAL'])
   #Add devcfg_main as an object in the devcfg image 
   devcfg_main_obj = env.Object('${BUILDPATH}/devcfg_main.c')
   env.Depends(devcfg_main_obj, primary_elf)
   devcfgimg_objs.extend(devcfg_main_obj)

   #=========================================================================
   # Beging building DEVCFG Image
   #-------------------------------------------------------------------------
   # # Create the devcfg linker script
   devcfg_lcs_file = env.DevcfgLCSBuilder(devcfg_lcs_name, devcfg_lcs_template_file, DEVCFG_DEF_HDR_FILE = devcfg_hdr_file_name)
   env.Depends(devcfg_lcs_file, devcfg_lcs_template_file)
   env.Depends(devcfg_lcs_file, (env.Value(env.get('DEVCFG_ENV_USED_XML_TAGS'))))
   install_devcfg_lcs_file = env.InstallAs(devcfg_lcs_file_path_name, devcfg_lcs_file)
   #devcfg_units.extend(devcfg_lcs_file_path_name)
   env.AddArtifact('DAL_DEVCFG_IMG', install_devcfg_lcs_file)



   # Create the devcfg_def hdr file that contains the segment addrs from the primary elf. This 
   # will be the address at which the devcfg elf data segments will be built at
   # Collect the segment name and the addr variable that was constructed when creating 
   # the devcfg lcs file: build\ms\DEVCFG_IMG.lcs.
   devcfg_scl_hdr_file = env.GenerateDevCfgHdrs(devcfg_hdr_file_name, [primary_elf])
   env.Depends(devcfg_scl_hdr_file, primary_elf)
   env.Depends(devcfg_scl_hdr_file, devcfg_lcs_file_path_name)
   install_devcfg_hdr_file = env.InstallAs(devcfg_hdr_file, devcfg_scl_hdr_file)
   devcfg_units.extend(install_devcfg_hdr_file)

# the following steps can only be performed if there is no filters in effect
if filter_opt is None:
   libs_path = env['INSTALL_LIBPATH']
   # create list of files used during link.

   #Preprocess the LCS (linker script) file
   pp_lcs = env.PreProcess(devcfg_lcs_file_path_name + '.pp', devcfg_lcs_file_path_name)
   #Cleanup the Preprocessed LCS file
   pp_lcs_clean = env.PreProcessClean(devcfg_lcs_file_path_name + '.i', pp_lcs)
   
   #Setup the bare minimum linker commands
   env.Replace(LINKFLAGS = "-m${Q6VERSION} -nostdlib --section-start .start=0x0 --entry=0x0 ${LFLAGS}")
   env.Append(LINKFLAGS = '-T' + str(pp_lcs_clean[0]))
   env.Replace(LINKOBJGRP = "--start-group $_LIBFLAGS_POSIX  $SOURCES.posix --end-group ")
   env.Replace(DYNLINKFLAGS = "")
   #Invoke the build command
   devcfg_elf = env.Program(devcfg_build_dest_name, 
                              source=[devcfgimg_objs], 
                              LIBS=[devcfgimg_libs], 
                              LIBPATH=libs_path)

   install_devcfg_elf = env.InstallAs(devcfg_elf_name, devcfg_elf)

   devcfg_map = env.SideEffect(env.subst(devcfg_build_dest_name + '.map'), devcfg_elf)
   devcfg_sym = env.SideEffect(env.subst(devcfg_build_dest_name + '.sym'), devcfg_elf)
   env.Clean(devcfg_elf, devcfg_map)
   env.Clean(devcfg_elf, devcfg_sym)
   
   env.Depends(pp_lcs_clean, devcfg_lcs_file_path_name)
   env.Depends(devcfg_elf, pp_lcs_clean)
   env.Depends(devcfg_elf, primary_elf)

   #-------------------------------------------------------------------------
   # Install ELF, reloc files
   #-------------------------------------------------------------------------
   # Merge elfs here
   # for SPD, corebsp standalone link directly with physical address; ADSP link with Virtual address, and use hexagon-objcopy to relocate
   # for MultiPD, corebsp standalone link with virtual address, and call scripts build from qurt to do the relocation; if follow the same steps to call qurt script, ADSP could not relocation here with hexagon-objcopy

   if 'SPD' in env:
   #   elfmergeout = env.ElfMergerUtilBuilder('${BUILD_MS_ROOT}/M${BUILD_ASIC}${BUILD_ID}${BUILD_VER}_v.elf', [primary_elf, devcfg_elf_name], 
   #                 PRIMARY_SECTION_NAME="DEVCFG_DATA", SECONDARY_SECTION_NAME="DEVCFG_DATA") 
      elfmergeout_pre_dummy_changes = env.ElfMergerUtilBuilder('${BUILD_MS_ROOT}/M${BUILD_ASIC}${BUILD_ID}${BUILD_VER}_pre_reloc_changes_v.elf', [primary_elf, devcfg_elf_name], 
                           PRIMARY_SECTION_NAME=[], SECONDARY_SECTION_NAME=[])
   
      #-------------------------------------------------------------------------
      # Add a dummy segment for dynamic footprints
      #-------------------------------------------------------------------------
      if 'USES_RELOC_ENABLE_AND_ALIGN' in env:
          env.AddUsesFlags('USES_DUMMY_SEG', from_builds_file = True)
      
      if 'USES_DUMMY_SEG' in env:
          if env.get('CHIPSET') == "mdm9x25":
              elfmergeout_pre_reloc_changes = elfmergeout_pre_dummy_changes
          else:
              elfmergeout_pre_reloc_changes = env.AddDummySegBuilder("${BUILD_ROOT}/dsp_with_dummy.elf", [elfmergeout_pre_dummy_changes, "${BUILD_ROOT}\${CHIPSET}\build\chipset\qdsp6.xml"])
      else:
            elfmergeout_pre_reloc_changes = elfmergeout_pre_dummy_changes
         
      if 'USES_RELOC_ENABLE_AND_ALIGN' in env:
           elfmergeout = env.ModifyRelocatableFlagsBuilder( '${BUILD_MS_ROOT}/M${BUILD_ASIC}${BUILD_ID}${BUILD_VER}_v', elfmergeout_pre_reloc_changes )
      else:
           elfmergeout = elfmergeout_pre_reloc_changes
       
       
      #install_merged_elf = env.InstallAs(merged_elf_name_output, elfmergeout)
      env.Depends(elfmergeout, (env.Value(env.get('SECTION_NAME_HDR_LIST'))))
      env.Depends(elfmergeout, devcfg_lcs_file)
      env.Depends(elfmergeout, install_devcfg_elf)
      install_target_lpass_reloc = env.Q6_LpassRelocBuilder(merged_elf_name_output, elfmergeout)
   else:
      elfmergeout = env.ElfMergerUtilBuilder(merged_elf_name, [primary_elf, devcfg_elf_name], 
                        PRIMARY_SECTION_NAME=[], SECONDARY_SECTION_NAME=[])

      env.Depends(elfmergeout, (env.Value(env.get('SECTION_NAME_HDR_LIST'))))
      env.Depends(elfmergeout, devcfg_lcs_file)
      env.Depends(elfmergeout, install_devcfg_elf) 
      install_merged_elf = env.InstallAs(merged_elf_name_output, elfmergeout)
 
   #-------------------------------------------------------------------------
   # Build env mbn files
   #-------------------------------------------------------------------------
   if 'SPD' in env:
      target_mbn = env.MbnBuilder('${TARGET_NAME}', install_target_lpass_reloc, IMAGE_TYPE="qdsp6sw", FLASH_TYPE=env['TARGET_FLASH_TYPE'])
      target_elf = env.InstallAs('${BUILD_ROOT}/dsp.elf', target_mbn)
      obj_mbn1 = env.InstallAs('${BUILD_ROOT}/obj/qdsp6v5_ReleaseG/dsp1.mbn', target_mbn)
      obj_mbn2 = env.InstallAs('${BUILD_ROOT}/obj/qdsp6v5_ReleaseG/dsp2.mbn', target_mbn)
      obj_mbn3 = env.InstallAs('${BUILD_ROOT}/obj/qdsp6v5_ReleaseG/dsp3.mbn', target_mbn)

   #=========================================================================
   # Define targets needed DEVCFGIMG
   #=========================================================================
   if 'SPD' in env:
      devcfg_units += [
         install_devcfg_elf,
         elfmergeout,
         install_target_lpass_reloc,
         target_mbn,
         target_elf,
         obj_mbn1,
         obj_mbn2,
         obj_mbn3,
      ]

      # add aliases
      for alias in aliases:
         env.Alias(alias, devcfg_units)
